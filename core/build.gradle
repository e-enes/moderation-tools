plugins {
    id 'maven-publish'
}

dependencies {
    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'org.xerial:sqlite-jdbc:3.45.2.0'
    implementation 'mysql:mysql-connector-java:8.0.33'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'

    implementation 'org.slf4j:slf4j-api:2.0.13'
    implementation 'ch.qos.logback:logback-classic:1.5.6'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test', Test) {
    useJUnitPlatform()
}

tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

tasks.register('javadocJar', Jar) {
    from tasks.javadoc
    archiveClassifier.set('javadoc')
}

publishing {
    repositories {
        maven {
            url = uri("https://maven.pkg.github.com/e-enes/moderation-tools")

            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            groupId = 'com.github.e-enes'
            artifactId = 'moderation-core'
            version = '1.0.0'

            from components.java

            artifact tasks.named('sourcesJar')
            artifact tasks.named('javadocJar')

            pom {
                name = "ModerationTools Core"
                description = "Core module for ModerationTools"
                url = "https://github.com/e-enes/moderation-tools/tree/main/core"

                scm {
                    connection = 'scm:git:git://github.com/e-enes/moderation-tools.git'
                    developerConnection = 'scm:git:ssh://github.com/e-enes/moderation-tools.git'
                    url = 'https://github.com/e-enes/moderation-tools'
                }
            }
        }
    }
}

tasks.withType(Jar).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}