plugins {
    id 'java'
    id 'checkstyle'
}

compileJava {
    enabled = false
}

jar {
    enabled = false
}

tasks.register('installGitHooks', Copy) {
    from "${rootProject.projectDir}/config/githooks"
    into "${rootProject.rootDir}/.git/hooks"
    fileMode 0755
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'

    repositories {
        mavenCentral()
    }

    group 'gg.enes.moderation.' + project.getName()

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    jar {
        archiveBaseName = project.getName() + '.ModerationTools'
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        dependsOn ':installGitHooks'
    }

    checkstyle {
        toolVersion = '10.16.0'
        ignoreFailures = false
    }
}
